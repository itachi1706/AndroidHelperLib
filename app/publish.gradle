apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

version android.defaultConfig.versionName
group 'com.itachi1706.helpers'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Android Helper Library'
            description 'Various Helper classes for Android Projects'
            url 'https://github.com/itachi1706/AndroidHelperLib'
            inceptionYear '2016'

            packaging 'aar'
            groupId this.group
            artifactId 'helperlib'
            version this.version

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/itachi1706/AndroidHelperLib.git'
                url 'https://github.com/itachi1706/AndroidHelperLib'

            }
            developers {
                developer {
                    id = 'itachi1706'
                    name 'Kenneth'
                }
            }
        }
    }
}

bintray {
    Properties localProps = new Properties()
    def localPropFile = file('../local.properties')
    if (localPropFile.canRead()) { localProps.load(new FileInputStream(localPropFile)) }

    user = localProps != null && localProps.containsKey('bintray.user') ? localProps.getProperty('bintray.user') : System.getenv('BINTRAY_USER')
    key = localProps != null && localProps.containsKey('bintray.apikey') ? localProps.getProperty('bintray.apikey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'ccn-android-lib'
        name = 'helper-lib-android'
        userOrg = 'itachi1706'
        description = "Various Helper classes for Android Projects"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/itachi1706/AndroidHelperLib.git'
        dryRun = false
        version {
            name = this.version
            desc = "Release ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}

artifactoryPublish.dependsOn('build')
publishing {
    publications {
        aar(MavenPublication) {
            // from components.java
            groupId = this.group
            artifactId = 'helperlib'
            version = this.version
            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.aar)
}

artifactory {
    Properties localProps = new Properties()
    def localPropFile = file('../local.properties')
    if (localPropFile.canRead()) { localProps.load(new FileInputStream(localPropFile)) }

    def userStr = localProps != null && localProps.containsKey('artifactory.user') ? localProps.getProperty('artifactory.user') : System.getenv('ARTIFACTORY_USER')
    def passwordStr = localProps != null && localProps.containsKey('artifactory.password') ? localProps.getProperty('artifactory.password') : System.getenv('ARTIFACTORY_PASSWORD')
    def urlStr = localProps != null && localProps.containsKey('artifactory.contextUrl') ? localProps.getProperty('artifactory.contextUrl') : System.getenv('ARTIFACTORY_CONTEXTURL')

    contextUrl = "${urlStr}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'ccn-android-libs'
            username = "${userStr}"
            password = "${passwordStr}"
            maven = true

        }
    }
    resolve {
        repository {
            repoKey = 'ccn-android-libs-virt'
            username = "${userStr}"
            password = "${passwordStr}"
            maven = true

        }
    }
}